/* Regression test for bug reported by Mathias Helsen, nov 2013
   region used only for output influences simulation.
*/

Nx := 256
Ny := 256
Nz := 1

setgridsize(Nx, Ny, Nz)
thickness := 20.0e-9
diameter := 1.0e-6
setcellsize(diameter/Nx, diameter/Ny, thickness/Nz)
setgeom(cylinder(diameter, thickness))

m = vortex(1, 1)

Msat = 860.0e3
Aex = 12e-12
alpha = 3

runwhile(maxtorque > 5e-4)

alpha = 0.01
A := 10*1.0e-3
f := 6.88495e9

mSavetime := 1.0/(8.0*f)

t0 := t 

B_ext = vector( 0.0, 0.0, A*sin(2*pi*f*(t-t0))*(1.0 - exp(-2*pi*f*(t-t0))) )

spot := circle(diameter/50.0).transl(diameter/5.0, diameter/7.0, 0.0)
defregion(1, spot)

run(0.5e-9)

m_ := average(m.region(1))
tol := 2e-3
expect("mx", m_[0], -0.4543135506766183, tol)
expect("my", m_[1],  0.8869750613257998, tol)
expect("mz", m_[2],  0.0804117293584914, tol)

run(0.5e-9)

m_ = average(m.region(1))
expect("mx", m_[0], -0.7082362402053106, tol)
expect("my", m_[1],  0.6975658053443545, tol)
expect("mz", m_[2], -0.1068760781061081, tol)


