/*
   Test if DMI tensor can be used to define an interfacially induced DMI
*/

setgridsize(64, 64, 1)
setcellsize(0.1, 0.1, 0.1)

enabledemag = false
AnisU = vector(0, 0, 1)
Aex = 1
Msat = 1
Ku1 = 1
DMI := 1.1

for BCtype := 0; BCtype < 3; BCtype++ {
	if BCtype == 0 {
		print("// Open BC...")
		openbc = true
	} else if BCtype == 1 {
		print("// Neumann BC...")
		openbc = false
	} else if BCtype == 2 {
		print("// Periodic BC...")
		SetPBC(1, 1, 0)
	}

	//// Dind
	Dbulk = 0
	print("Dind...")

	// Use DMI tensor...
	ext_DMItensor(0, 0, 2, DMI)
	ext_DMItensor(0, 2, 0, -DMI)
	ext_DMItensor(1, 1, 2, DMI)
	ext_DMItensor(1, 2, 1, -DMI)
	Dind = 0
	m = neelskyrmion(1, -1)
	relax()
	minimize()
	Etot_dmitensor_ind := E_total.get()
	m_dmitensor_ind := m.getCell(40, 36, 0)

	// Use Dind...
	ext_DMItensor(0, 0, 2, 0.0)
	ext_DMItensor(0, 2, 0, 0.0)
	ext_DMItensor(1, 1, 2, 0.0)
	ext_DMItensor(1, 2, 1, 0.0)
	Dind = DMI
	m = neelskyrmion(1, -1)
	relax()
	minimize()
	Etot_dind := E_total.get()
	m_dind := m.getCell(40, 36, 0)

	// Are their results equal?
	Expect("DMI tensor vs. Dind | Etot", Etot_dmitensor_ind, Etot_dind, 1e-5*abs(Etot_dind))
	ExpectV("DMI tensor vs. Dind | m", m_dmitensor_ind, m_dind, 1e-3)

	//// Dbulk
	Dind = 0
	print("Dbulk...")
	// Use DMI tensor...
	ext_DMItensor(1, 0, 2, -DMI)
	ext_DMItensor(1, 2, 0, DMI)
	ext_DMItensor(0, 1, 2, DMI)
	ext_DMItensor(0, 2, 1, -DMI)
	ext_DMItensor(2, 1, 0, -DMI)
	ext_DMItensor(2, 0, 1, DMI)
	Dbulk = 0
	m = neelskyrmion(1, -1)
	relax()
	minimize()
	Etot_dmitensor_bulk := E_total.get()
	m_dmitensor_bulk := m.getCell(40, 36, 0)

	// Use Dind...
	ext_DMItensor(1, 0, 2, 0.0)
	ext_DMItensor(1, 2, 0, 0.0)
	ext_DMItensor(0, 1, 2, 0.0)
	ext_DMItensor(0, 2, 1, 0.0)
	ext_DMItensor(2, 1, 0, 0.0)
	ext_DMItensor(2, 0, 1, 0.0)
	Dbulk = DMI
	m = neelskyrmion(1, -1)
	relax()
	minimize()
	Etot_dbulk := E_total.get()
	m_dbulk := m.getCell(40, 36, 0)

	// Are their results equal?
	Expect("DMI tensor vs. Dbulk | Etot", Etot_dmitensor_bulk, Etot_dbulk, 1e-5*abs(Etot_dbulk))
	ExpectV("DMI tensor vs. Dbulk | m", m_dmitensor_bulk, m_dbulk, 1e-3)
}
